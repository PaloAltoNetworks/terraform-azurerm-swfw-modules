# Palo Alto Networks Route Server Module for Azure

A Terraform module for deploying a Route Server and its components required for the VM-Series firewalls in Azure.

## Usage

In order to use `route_server` module, you need to deploy `azurerm_resource_group` and use module `vnet` as
prerequisites.
Then you can use below code as an example of calling module to create a Route Server:

```hcl
module "route_server" {
  source = "PaloAltoNetworks/swfw-modules/azurerm//modules/route_server"

  for_each = var.route_servers

  name                = "${var.name_prefix}${each.value.name}"
  resource_group_name = local.resource_group.name
  region              = var.region

  subnet_id = "/subscription/xxxx/......."

  zones     = each.value.zones
  public_ip = each.value.public_ip

  sku                      = each.value.sku
  branch_to_branch_traffic = each.value.branch_to_branch_traffic
  bgp_connections          = each.value.bgp_connections

  tags = var.tags
}
```

Below there are provided sample values for `route_servers` map:

```hcl
route_servers = {
  "routeserver" = {
    name       = "routeserver"
    vnet_key   = "transit"
    subnet_key = "routeserver"
    public_ip = {
      create = true
      name   = "routeserver-pip"
    }
    bgp_connections = {
      "vmseries" = {
        name     = "ebgp-to-vmseries"
        peer_asn = "64512"
        peer_ip  = "1.1.1.1"
      }
    }
  }
}
```

To make defining the Route Servers easy, you can use the following variable in the *glue code*:

```hcl
variable "route_servers" {
  description = "Map of Route Servers to create."
  default     = {}
  nullable    = false
  type = map(object({
    name       = string
    vnet_key   = string
    subnet_key = string
    zones      = optional(list(string))
    public_ip = object({
      create              = bool
      name                = optional(string)
      resource_group_name = optional(string)
      key                 = optional(string)
    })
    sku                      = optional(string, "Standard")
    branch_to_branch_traffic = optional(bool, true)
    bgp_connections = optional(map(object({
      name     = string
      peer_asn = string
      peer_ip  = string
    })), {})
  }))
}
```