# Palo Alto Networks Virtual Network Gateway Module for Azure

A terraform module for deploying a VNG (Virtual Network Gateway) and its components required for the VM-Series firewalls in Azure.

## Usage

In order to use module `virtual_network_gateway`, you need to deploy `azurerm_resource_group` and use module `vnet` as
prerequisites.
Then you can use below code as an example of calling module to create VNG:

```hcl
module "vng" {
  source = "PaloAltoNetworks/swfw-modules/azurerm//modules/virtual_network_gateway"

  for_each = var.virtual_network_gateways

  name                = "${var.name_prefix}${each.value.name}"
  region              = var.region
  resource_group_name = local.resource_group.name

  ip_configurations = each.value.ip_configurations
  instance_settings = each.value.instance_settings
  subnet_id         = "/subscription/xxxx/......."

  azure_bgp_peer_addresses = each.value.azure_bgp_peer_addresses
  bgp                      = each.value.bgp
  local_network_gateways   = each.value.local_network_gateways
  vpn_clients              = each.value.vpn_clients

  tags = var.tags
}
```

Below there are provided sample values for `virtual_network_gateways` map:

```hcl
virtual_network_gateways = {
  expressroute = {
    name = "expressroute"
    instance_settings = {
      type = "ExpressRoute"
      # vpn_type = "PolicyBased"
      sku = "Standard"
      # generation = "Generation1"
    }
    vnet_key   = "transit"
    subnet_key = "vpn"
    ip_configurations = {
      primary = {
        create_public_ip = true
        name             = "primary"
        public_ip_name   = "expressroute_pip"
      }
    }

  }
  expressroute_policy_based = {
    name = "er_policy"
    instance_settings = {
      type       = "ExpressRoute"
      vpn_type   = "PolicyBased"
      sku        = "Standard"
      generation = "Generation2"
    }
    vnet_key   = "er"
    subnet_key = "vpn"
    ip_configurations = {
      primary = {
        create_public_ip = true
        name             = "primary"
        public_ip_name   = "er_policy_pip"
      }
    }

  }
  vpn_simple = {
    name = "simple-vpn"
    instance_settings = {
      type = "Vpn"
      # vpn_type   = "PolicyBased"
      sku        = "VpnGw1"
      generation = "Generation1"
    }
    vnet_key   = "er"
    subnet_key = "vpn"
    ip_configurations = {
      primary = {
        create_public_ip = true
        name             = "primary"
        public_ip_name   = "simple_vpn_pip"
      }
    }

  }
  "vng" = {
    name = "vng"
    instance_settings = {
      type          = "Vpn"
      sku           = "VpnGw2AZ"
      generation    = "Generation2"
      active_active = true
    }
    vnet_key   = "transit"
    subnet_key = "vpn"
    ip_configurations = {
      primary = {
        name             = "primary"
        create_public_ip = true
        public_ip_name   = "vng-primary-pip"
      }
      secondary = {
        name             = "secondary"
        create_public_ip = true
        public_ip_name   = "vng-secondary-pip"
      }
    }

    azure_bgp_peer_addresses = {
      one_primary     = "169.254.21.2"
      one_secondary   = "169.254.22.2"
      two_primary     = "169.254.21.12"
      two_secondary   = "169.254.22.12"
      three_primary   = "169.254.21.22"
      three_secondary = "169.254.22.22"
    }
    bgp = {
      enable = true
      configuration = {
        asn = "65002"
        primary_peering_addresses = {
          name               = "primary"
          apipa_address_keys = ["one_primary", "two_primary", "three_primary"]
        }
        secondary_peering_addresses = {
          name               = "secondary"
          apipa_address_keys = ["one_secondary", "two_secondary", "three_secondary"]
        }
      }
    }
    local_network_gateways = {
      lg1 = {
        name            = "local_gw_1"
        gateway_address = "8.8.8.8"
        remote_bgp_settings = {
          asn                 = "65000"
          bgp_peering_address = "169.254.21.1"
        }
        connection = {
          name = "connection_1"
          custom_bgp_addresses = {
            primary_key   = "one_primary"
            secondary_key = "one_secondary"
          }
          mode       = "InitiatorOnly"
          shared_key = "test123"
          ipsec_policies = [
            {
              dh_group         = "ECP384"
              ike_encryption   = "AES256"
              ike_integrity    = "SHA256"
              ipsec_encryption = "AES256"
              ipsec_integrity  = "SHA256"
              pfs_group        = "ECP384"
              sa_datasize      = "102400000"
              sa_lifetime      = "14400"
            }
          ]
        }
      }
      lg2 = {
        name            = "local_gw_2"
        gateway_address = "4.4.4.4"
        remote_bgp_settings = {
          asn                 = "65000"
          bgp_peering_address = "169.254.22.1"
        }
        connection = {
          name = "connection_2"
          custom_bgp_addresses = {
            primary_key   = "two_primary"
            secondary_key = "two_secondary"
          }
          mode       = "InitiatorOnly"
          shared_key = "test123"
          ipsec_policies = [
            {
              dh_group         = "ECP384"
              ike_encryption   = "AES256"
              ike_integrity    = "SHA256"
              ipsec_encryption = "AES256"
              ipsec_integrity  = "SHA256"
              pfs_group        = "ECP384"
              sa_datasize      = "102400000"
              sa_lifetime      = "14400"
            }
          ]
        }
      }
    }
  }
}
```

To make defining the VNGs easy, you can use the following variable in *glue code*:

```hcl
variable "virtual_network_gateways" {
  description = "Map of virtual_network_gateways to create"
  default     = {}
  nullable    = false
  type = map(object({
    name = string
    instance_settings = object({
      type          = optional(string)
      vpn_type      = optional(string)
      sku           = optional(string)
      active_active = optional(bool)
      generation    = optional(string)
    })
    vnet_key   = string
    subnet_key = string

    ip_configurations = object({
      primary = object({
        name                          = string
        create_public_ip              = optional(bool)
        public_ip_name                = string
        private_ip_address_allocation = optional(string)
      })
      secondary = optional(object({
        name                          = string
        create_public_ip              = optional(bool)
        public_ip_name                = string
        private_ip_address_allocation = optional(string)
      }))
    })

    azure_bgp_peer_addresses = optional(map(string))
    bgp = optional(object({
      enable = optional(bool, false)
      configuration = optional(object({
        asn         = string
        peer_weight = optional(number)
        primary_peering_addresses = object({
          name               = string
          apipa_address_keys = list(string)
          default_addresses  = optional(list(string))
        })
        secondary_peering_addresses = optional(object({
          name               = string
          apipa_address_keys = list(string)
          default_addresses  = optional(list(string))
        }))
      }))
    }))
    local_network_gateways = optional(map(object({
      name = string
      remote_bgp_settings = optional(object({
        asn                 = string
        bgp_peering_address = string
        peer_weight         = optional(number)
      }))
      gateway_address = optional(string)
      address_space   = optional(list(string), [])
      connection = object({
        name = string
        custom_bgp_addresses = optional(object({
          primary_key   = string
          secondary_key = optional(string)
        }))
        ipsec_policies = list(object({
          dh_group         = string
          ike_encryption   = string
          ike_integrity    = string
          ipsec_encryption = string
          ipsec_integrity  = string
          pfs_group        = string
          sa_datasize      = optional(string)
          sa_lifetime      = optional(string)
        }))
        type       = optional(string)
        mode       = optional(string)
        shared_key = optional(string)
      })
    })), {})
    vpn_clients = optional(map(object({
      address_space         = string
      aad_tenant            = optional(string)
      aad_audience          = optional(string)
      aad_issuer            = optional(string)
      root_certificates     = optional(map(string), {})
      revoked_certificates  = optional(map(string), {})
      radius_server_address = optional(string)
      radius_server_secret  = optional(string)
      vpn_client_protocols  = optional(list(string))
      vpn_auth_types        = optional(list(string))
    })), {})
  }))
}
```