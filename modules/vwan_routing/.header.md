# Palo Alto Networks Virtual WAN Routing Module for Azure

A Terraform module for managing routing in an Azure Virtual WAN.

## Usage

In order to use the `vwan_routing module`, you need to deploy the `vwan` module as a prerequisite.
Then you can use the example below to call the module and configure routing in the Virtual Hub:

```hcl
module "vwan_routing" {
  source = "PaloAltoNetworks/swfw-modules/azurerm//modules/vhub_routing"
  routing_intent = merge(var.virtual_hub_routing.routing_intent, {
    routing_policy = [
      for policy in var.virtual_hub_routing.routing_intent.routing_policy : merge(policy, {
        next_hop_id = module.cloudngfw[policy.next_hop_key].palo_alto_virtual_network_appliance_id
      })
    ]
  })
  virtual_hub_id = module.virtual_wan[var.virtual_hub_routing.virtual_wan_key].virtual_hub_ids[var.virtual_hub_routing.virtual_hub_key]
}
```

Below there are provided sample values for `virtual_hub_routing` map:

```hcl
virtual_hub_routing = {
  virtual_wan_key = "virtual_wan"
  virtual_hub_key = "virtual_hub"
  routing_intent = {
    routing_intent_name = "routing_intent"
    routing_policy = [
      {
        routing_policy_name = "PrivateTraffic"
        destinations        = ["PrivateTraffic"]
        next_hop_key        = "cloudngfw"
      },
      {
        routing_policy_name = "Internet"
        destinations        = ["Internet"]
        next_hop_key        = "cloudngfw"
      }
    ]
  }
}
```

