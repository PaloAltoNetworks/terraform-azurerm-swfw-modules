# Palo Alto Networks Public IP Module for Azure

A Terraform module for deploying Public IP Addresses and Prefixes required for the VM-Series firewalls and other infrastructure
components in Azure.

## Purpose

All other modules that may require a Public IP Address resource support simple creation of it. This module allows more advanced
creation of Public IP Addresses and Prefixes, including:
- Specifying Availability Zones for Public IPs separately from the main resource
- Specifying Domain Name Label and Idle Timeout for Public IPs
- Allocating Public IP Addresses from an existing Public IP Prefix (e.g. Custom non-Microsoft IP Prefix)

## Usage

In order to use module `public_ip`, you need to deploy `azurerm_resource_group` as a prerequisite. Then you can use below code as
an example of calling module to create Public IP Addresses and Prefixes:

```hcl
module "public_ip" {
  source = ""PaloAltoNetworks/swfw-modules/azurerm//modules/public_ip"

  region = var.region
  public_ip_addresses = {
    for k, v in var.public_ips.public_ip_addresses : k => merge(v, {
      name                = "${var.name_prefix}${v.name}"
      resource_group_name = coalesce(v.resource_group_name, local.resource_group.name)
    })
  }
  public_ip_prefixes = {
    for k, v in var.public_ips.public_ip_prefixes : k => merge(v, {
      name                = "${var.name_prefix}${v.name}"
      resource_group_name = coalesce(v.resource_group_name, local.resource_group.name)
    })
  }

  tags = var.tags
}
```

Below there are provided sample values for `public_ips` map:

```hcl
public_ips = {
  public_ip_addresses = {
    # create a new public ip address in zone 1, with domain name label "domainname" and idle timeout of 10 minutes
    pip1 = {
      create                  = true
      name                    = "new-public-ip-name1"
      resource_group_name     = "pip-rg-name"
      zones                   = ["1"]
      domain_name_label       = "domainname"
      idle_timeout_in_minutes = 10
    }
    # create a new public ip address in all zones (default) and allocate it from an existing ip prefix
    pip2 = {
      create                     = true
      name                       = "new-public-ip-name2"
      resource_group_name        = "pip-rg-name"
      prefix_name                = "public-ip-prefix-name"
      prefix_resource_group_name = "ippre-rg-name"
    }
    # source an existing public ip address
    pip3 = {
      create              = false
      name                = "existing-public-ip-name"
      resource_group_name = "pip-rg-name"
    }
  }
  public_ip_prefixes = {
    # create a new public ip prefix in zone 1 with a prefix length of /28 (default)
    ippre1 = {
      create              = true
      name                = "new-public-ip-prefix-name1"
      resource_group_name = "ippre-rg-name"
      zones               = ["1"]
    }
    # create a new public ip prefix in all zones (default) with a prefix length of /30
    ippre2 = {
      create              = true
      name                = "new-public-ip-prefix-name2"
      resource_group_name = "ippre-rg-name"
      length              = 30
    }
    # source and existing public ip prefix
    ippre3 = {
      create              = false
      name                = "existing-public-ip-prefix-name"
      resource_group_name = "ippre-rg-name"
    }
  }
}
```

To make defining the Public IPs easy, you can use the following variable in the _glue_ code:

```hcl
variable "public_ips" {
  description = "A map defining Public IP Addresses and Prefixes."
  default     = {}
  type = object({
    public_ip_addresses = optional(map(object({
      create                     = bool
      name                       = string
      resource_group_name        = optional(string)
      zones                      = optional(list(string))
      domain_name_label          = optional(string)
      idle_timeout_in_minutes    = optional(number)
      prefix_name                = optional(string)
      prefix_resource_group_name = optional(string)
    })), {})
    public_ip_prefixes = optional(map(object({
      create              = bool
      name                = string
      resource_group_name = optional(string)
      zones               = optional(list(string))
      length              = optional(number)
    })), {})
  })
}
```