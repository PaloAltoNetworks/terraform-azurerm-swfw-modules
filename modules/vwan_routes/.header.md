# Palo Alto Networks Virtual WAN Routing Module for Azure

A Terraform module for managing routing in an Azure Virtual WAN.

## Usage

In order to use the `vwan_routes` module, you need to deploy the `vwan` module as a prerequisite.
Then you can use the example below to call the module and configure routing in the Virtual Hub:

```hcl
locals {
  routing_intent = {
    for vwan_key, vwan in var.virtual_wans : vwan_key => {
      for hub_key, hub in try(vwan.virtual_hubs, {}) : hub_key => {
        virtual_hub_id = try(
          module.virtual_wan[vwan_key].virtual_hub_ids[hub_key],
          null
        )
        routing_intent = {
          routing_intent_name = hub.routing_intent.routing_intent_name
          routing_policy = [
            for policy in hub.routing_intent.routing_policy : merge(
              policy,
              {
                next_hop_id = try(
                  module.cloudngfw[policy.next_hop_key]
                  .palo_alto_virtual_network_appliance_id,
                  null
                )
              }
            )
          ]
        }
      }
      if hub.routing_intent != null
    }
  }

  routes = {
    for vwan_key, vwan in var.virtual_wans : vwan_key => {
      for route_item in flatten([
        for hub_key, hub in try(vwan.virtual_hubs, {}) : [
          for rt_key, rt in try(hub.route_tables, {}) : [
            for route_key, route in try(rt.routes, {}) : {
              route_key         = route_key
              name              = route.name
              destinations_type = route.destinations_type
              destinations      = route.destinations
              next_hop_type     = route.next_hop_type
              next_hop_key      = try(route.next_hop_key, null)
              route_table_key   = rt_key
              hub_key           = hub_key
            }
          ]
        ]
        ]) : route_item.route_key => {
        name              = route_item.name
        destinations_type = route_item.destinations_type
        destinations      = route_item.destinations
        next_hop_type     = route_item.next_hop_type
        next_hop_id = try(
          module.cloudngfw[route_item.next_hop_key].palo_alto_virtual_network_appliance_id,
          null
        )
        route_table_id = try(
          module.virtual_wan[vwan_key].route_table_ids[route_item.route_table_key],
          null
        )
      }
    }
  }
}

module "vwan_routes" {
  source   = "../../modules/vwan_routes"
  for_each = var.virtual_wans

  routes         = lookup(local.routes, each.key, {})
  routing_intent = lookup(local.routing_intent, each.key, {})
}
```

Below there are provided sample values:

```hcl
      "virtual_hub_routes" = {
        name           = "virtual_hub_routes"
        address_prefix = "11.0.0.0/24"
        route_tables = {
          "route_table" = {
            name   = "route_table"
            labels = ["rt_label01", "rt_label02"]
            routes = {
              "route01" = {
                name              = "route01"
                destinations_type = "CIDR"
                destinations      = ["10.0.0.0/16"]
                next_hop_key      = "ha-ports"
              }
              "route02" = {
                name              = "route02"
                destinations_type = "CIDR"
                destinations      = ["11.0.0.0/16"]
                next_hop_key      = "ha-ports"
              }
            }
          }
        }
      }
      "virtual_hub_routing_intent" = {
        name           = "virtual_hub_routing_intent"
        address_prefix = "12.0.0.0/24"
        routing_intent = {
          routing_intent_name = "routing_intent"
          routing_policy = [
            {
              routing_policy_name = "PrivateTraffic"
              destinations        = ["PrivateTraffic"]
              next_hop_key        = "cloudngfw"
            },
            {
              routing_policy_name = "Internet"
              destinations        = ["Internet"]
              next_hop_key        = "cloudngfw"
            }
          ]
        }
      }
```

